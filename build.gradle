buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.sarhanm:gradle-versioner:2.1.20'

        //Leave at version 0.9.1 for now. Looks like the latest version
        //has dependency issues that prevents a successful publish
        // with gradle version 2.8+
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: 'com.sarhanm.versioner'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "jacoco"

versioner{
    //Community Plugins are required to have a clean version string.
    omitBranchMetadataOnSolidBranch=true
}

group = 'com.sarhanm'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile gradleTestKit()

    compile 'org.yaml:snakeyaml:1.17'
    compile ('org.codehaus.groovy.modules.http-builder:http-builder:0.7.2'){
        exclude module: 'nekohtml'
    }

    compile ('org.spockframework:spock-core:1.0-groovy-2.4'){
        exclude group: 'org.codehaus.groovy'
    }

    testCompile 'com.netflix.nebula:nebula-test:4.2.2'
}

apply plugin: 'com.jfrog.bintray'

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task groovydocJar( type: Jar ) {
    classifier 'javadoc'
    from groovydoc
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
        }
    }
}

bintray {
    user = 'sarhanm'
    key = System.env.BINTRAYTOKEN
    publications = ['mavenJava']
    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'gradle-versioner'
        desc = 'A gradle plugin to set the project version based on git metadata'
        websiteUrl = 'https://github.com/sarhanm/gradle-versioner'
        issueTrackerUrl = 'https://github.com/sarhanm/gradle-versioner/issues'
        vcsUrl = 'https://github.com/sarhanm/gradle-versioner.git'
        licenses = ['MIT']
        labels = ['gradle', 'git', 'versioning', 'auto version' ,'versioner', 'version']
        publicDownloadNumbers = true
        version {
            name = project.version
            attributes = ['gradle-plugin': ['com.sarhanm.versioner:com.sarhanm:gradle-versioner','com.sarhanm.version-resolver:com.sarhanm:gradle-versioner'] ]
        }
    }
}



pluginBundle {
  website = 'https://github.com/sarhanm/gradle-versioner/wiki'
  vcsUrl = 'https://github.com/sarhanm/gradle-versioner'

  description = 'Set the project version based on git metadata and number of commits.'
  tags = ['versioner', 'version', 'git', 'scm', 'commits', 'auto version', 'auto', 'gradle']

  plugins {

    versionerPlugin {
      id = 'com.sarhanm.versioner'
      displayName = 'Gradle Project Version Set Plugin'
      description = 'Set the project version based on git metatdata and number of commits. Example: 1.0.81.master.efbb95a'
      //tags = ['versioner', 'version', 'git', 'scm', 'commits', 'auto version', 'auto', 'gradle']
    }

    versionResolverPlugin {
      id = 'com.sarhanm.version-resolver'
      displayName = 'Version Resolver: Resolve Dependencies From Global Manifest'
      description = 'Allows you to resolve versions for all dependencies from a global manifest rather than hard coding versions in each build.gradle file'
      //tags = ['versioner', 'version', 'resolve', 'yaml', 'manifest']
    }
  }
}

/**
 * Grabs the values from the env and writes to a file that can be used by the plugin-publish plugin.
 * This allows us define the API and secrete securely in travis-ci
 */
task setupPluginUpload << {

    def key=System.env.gradlePublishKey
    def secret = System.env.gradlePublishSecret

    if( !key || !secret)
    {
        throw new RuntimeException("gradlePublishKey and/or gradlePublishSecret are not defined environment variables")
    }

    System.properties.setProperty("gradle.publish.key", key)
    System.properties.setProperty("gradle.publish.secret", secret)
}

tasks.publishPlugins.dependsOn tasks.setupPluginUpload

// If we want our unit tests to work in intellij/eclipse, then we need to change
// where our build output goes so its the same on the command line and in intellij.
// This is because the java-gradle-plugin generates a meta-data file used in testing,
// and that meta-data files adds build/classes/main to the classpath.
if(System.properties.getProperty("local-eclipse", "false") == "true") {
    project.plugins.withType(EclipsePlugin) {
        project.eclipse {
            classpath {
                defaultOutputDir = project.file("${project.buildDir}/classes/main")
            }
        }
    }
}
